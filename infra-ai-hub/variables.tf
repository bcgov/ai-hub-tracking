variable "location" {
  type        = string
  description = <<DESCRIPTION
Azure region where all resources should be deployed.

This specifies the primary Azure region for deploying the AI/ML landing zone infrastructure. All resources will be created in this region unless specifically configured otherwise in individual resource definitions.
DESCRIPTION
  nullable    = false
}

variable "subscription_id" {
  type        = string
  description = <<DESCRIPTION
Azure subscription ID where resources will be deployed.

If not specified, will use the subscription from Azure CLI context (az account show).
Can also be set via ARM_SUBSCRIPTION_ID environment variable.
DESCRIPTION
  nullable    = false
}

# This is required for most resource modules
variable "resource_group_name" {
  type        = string
  description = <<DESCRIPTION
The name of the resource group where all resources will be deployed.

This resource group will contain all the AI/ML landing zone infrastructure components. The resource group should already exist or will be created as part of the deployment process.
DESCRIPTION
}

variable "flag_platform_landing_zone" {
  type        = bool
  default     = true
  description = <<DESCRIPTION
Flag to indicate if the platform landing zone is enabled.

If set to true, the module will deploy resources and connect to a platform landing zone hub. This enables integration with existing hub-and-spoke network architectures and centralized management services.
DESCRIPTION
}

variable "enable_telemetry" {
  type        = bool
  default     = false
  description = <<DESCRIPTION
This variable controls whether or not telemetry is enabled for the module.
For more information see <https://aka.ms/avm/telemetryinfo>.
If it is set to false, then no telemetry will be collected.
DESCRIPTION
  nullable    = false
}

variable "name_prefix" {
  type        = string
  default     = null
  description = <<DESCRIPTION
Optional prefix to be used for naming resources.

This prefix will be applied to all resource names generated by the module. It is useful for ensuring consistent naming conventions across deployments without requiring explicit names for each resource. The prefix should be kept under 10 characters and use only alphanumeric lowercase characters to avoid Azure naming limitations.
DESCRIPTION

  validation {
    condition = var.name_prefix == null || (
      length(var.name_prefix) <= 10 &&
      can(regex("^[a-z0-9]+$", var.name_prefix))
    )
    error_message = "The name_prefix must contain only lowercase alphanumeric characters and be 10 characters or less or remain null."
  }
}

variable "tags" {
  type        = map(string)
  default     = null
  description = <<DESCRIPTION
Map of tags to be assigned to all resources created by this module.

Tags are key-value pairs that help organize and manage Azure resources. These tags will be applied to all resources created by the module, enabling consistent resource governance, cost tracking, and operational management across the AI/ML landing zone infrastructure.
DESCRIPTION
}

variable "private_endpoints" {
  type = object({
    enabled                = optional(bool, false)
    location               = optional(string, null)
    vnet_resource_id       = optional(string, null)
    subnet_name            = optional(string, "PrivateEndpointSubnet")
    private_dns_zone_rg_id = optional(string, null)
    resource_group_name    = optional(string, null)
  })
  default = {
    enabled = false
  }
  description = <<DESCRIPTION
Configuration for creating private endpoints for AI Foundry and its dependencies.

Supports both same-region and cross-region private endpoint scenarios for disaster recovery 
and multi-region access patterns.

- `enabled` - (Optional) Whether to create private endpoints. Default is false.
- `location` - (Optional) Azure region where private endpoints will be created. Required if enabled is true.
- `vnet_resource_id` - (Optional) Resource ID of the VNet where private endpoints will be created. Required if enabled is true.
- `subnet_name` - (Optional) Name of the subnet within the VNet for private endpoints. Default is "PrivateEndpointSubnet".
- `private_dns_zone_rg_id` - (Optional) Resource ID of the resource group containing private DNS zones for private endpoint DNS records. If not provided, DNS integration will be skipped.
- `resource_group_name` - (Optional) Name of the resource group where private endpoints will be created. If not provided, will use the same resource group as the main resources.
DESCRIPTION

  validation {
    condition = !var.private_endpoints.enabled || (
      (var.private_endpoints.vnet_resource_id != null || var.private_endpoints_vnet_resource_id != null) &&
      var.private_endpoints.location != null
    )
    error_message = "Both vnet_resource_id (either private_endpoints.vnet_resource_id or private_endpoints_vnet_resource_id) and location are required when private_endpoints is enabled."
  }
}

variable "private_endpoints_vnet_resource_id" {
  type        = string
  default     = null
  description = <<DESCRIPTION
Alternative to private_endpoints.private_dns_zone_rg_id
Use this variable if you want to set the value using an environment variable or other means.
DESCRIPTION
}

variable "private_endpoints_dns_zone_rg_id" {
  type        = string
  default     = null
  description = <<DESCRIPTION
Alternative to private_endpoints.private_dns_zone_rg_id
Use this variable if you want to set the value using an environment variable or other means.
DESCRIPTION
}

variable "private_dns_zones_rg_id" {
  type        = string
  default     = null
  description = <<DESCRIPTION
Alternative to private_dns_zones.existing_zones_resource_group_resource_id
Use this variable if you want to set the value using an environment variable or other means.

DESCRIPTION
}

variable "private_dns_zones" {
  type = object({
    existing_zones_resource_group_resource_id = optional(string, null)
  })
  default = {
    existing_zones_resource_group_resource_id = null
  }
  description = <<DESCRIPTION
Configuration for private DNS zones used with private endpoints.

- `existing_zones_resource_group_resource_id` - (Optional) Resource ID of an existing resource group containing private DNS zones. Required when flag_platform_landing_zone is false.

This variable is used to reference existing private DNS zones for private endpoint DNS integration when not using the platform landing zone pattern.
DESCRIPTION

  validation {
    # Allow null (skip DNS integration) OR require valid resource group ID format
    condition = var.flag_platform_landing_zone || (
      (var.private_dns_zones.existing_zones_resource_group_resource_id == null && var.private_dns_zones_rg_id == null) ||
      can(regex("^/subscriptions/[^/]+/resourceGroups/[^/]+$", coalesce(var.private_dns_zones.existing_zones_resource_group_resource_id, var.private_dns_zones_rg_id, "")))
    )
    error_message = "When providing existing_zones_resource_group_resource_id, it must be a valid resource group resource ID in the format: /subscriptions/{subscriptionId}/resourceGroups/{resourceGroupName}"
  }
}

variable "law_definition" {
  type = object({
    resource_id = optional(string, null)
    name        = optional(string, null)
    retention   = optional(number, 30)
    sku         = optional(string, "PerGB2018")
  })
  default = {
    resource_id = null
  }
  description = <<DESCRIPTION
Configuration for Log Analytics Workspace.

- `resource_id` - (Optional) Resource ID of an existing Log Analytics Workspace to use.
- `name` - (Optional) Name for a new Log Analytics Workspace.
- `retention` - (Optional) Data retention in days. Default is 30. Must be between 30 and 730 days.
- `sku` - (Optional) Workspace SKU. Default is "PerGB2018".
DESCRIPTION

  validation {
    condition     = var.law_definition.retention >= 30 && var.law_definition.retention <= 730
    error_message = "Log Analytics Workspace retention must be between 30 and 730 days."
  }
}


## Passing most of the Foundry Pattern module's inputs in here to allow for flexibility.
#TODO: remove DNS zone ID's injection for everything.  This comes in as an RG id where the
variable "ai_foundry_definition" {
  type = object({
    # AI Foundry Hub Configuration
    create_byor      = optional(bool, true)
    purge_on_destroy = optional(bool, false)
    ai_foundry = optional(object({
      name                     = optional(string, null)
      disable_local_auth       = optional(bool, false)
      allow_project_management = optional(bool, true)
      create_ai_agent_service  = optional(bool, false)
      #network_injections is statically set to vnet/subnet created in the module.
      private_dns_zone_resource_ids = optional(list(string), [])
      sku                           = optional(string, "S0")
      role_assignments = optional(map(object({
        role_definition_id_or_name             = string
        principal_id                           = string
        description                            = optional(string, null)
        skip_service_principal_aad_check       = optional(bool, false)
        condition                              = optional(string, null)
        condition_version                      = optional(string, null)
        delegated_managed_identity_resource_id = optional(string, null)
        principal_type                         = optional(string, null)
      })), {})
    }), {})
    #AI model configurations
    ai_model_deployments = optional(map(object({
      name                   = string
      rai_policy_name        = optional(string)
      version_upgrade_option = optional(string, "OnceNewDefaultVersionAvailable")
      model = object({
        format  = string
        name    = string
        version = string
      })
      scale = object({
        capacity = optional(number)
        family   = optional(string)
        size     = optional(string)
        tier     = optional(string)
        type     = string
      })
    })), {})
    # AI Projects Configuration
    ai_projects = optional(map(object({
      name                       = string
      sku                        = optional(string, "S0")
      display_name               = string
      description                = string
      create_project_connections = optional(bool, false)
      cosmos_db_connection = optional(object({
        existing_resource_id = optional(string, null)
        new_resource_map_key = optional(string, null)
      }), {})
      ai_search_connection = optional(object({
        existing_resource_id = optional(string, null)
        new_resource_map_key = optional(string, null)
      }), {})
      key_vault_connection = optional(object({
        existing_resource_id = optional(string, null)
        new_resource_map_key = optional(string, null)
      }), {})
      storage_account_connection = optional(object({
        existing_resource_id = optional(string, null)
        new_resource_map_key = optional(string, null)
      }), {})
    })), {})
    # Bring Your Own Resources (BYOR) Configuration
    # One or more AI search installations.
    ai_search_definition = optional(map(object({
      existing_resource_id         = optional(string, null)
      name                         = optional(string)
      private_dns_zone_resource_id = optional(string, null)
      enable_diagnostic_settings   = optional(bool, true)
      sku                          = optional(string, "standard")
      local_authentication_enabled = optional(bool, true)
      partition_count              = optional(number, 1)
      replica_count                = optional(number, 2)
      semantic_search_sku          = optional(string, "standard")
      semantic_search_enabled      = optional(bool, false)
      hosting_mode                 = optional(string, "default")
      tags                         = optional(map(string), {})
      role_assignments = optional(map(object({
        role_definition_id_or_name             = string
        principal_id                           = string
        description                            = optional(string, null)
        skip_service_principal_aad_check       = optional(bool, false)
        condition                              = optional(string, null)
        condition_version                      = optional(string, null)
        delegated_managed_identity_resource_id = optional(string, null)
        principal_type                         = optional(string, null)
      })), {})
      enable_telemetry = optional(bool, true)
    })), {})

    cosmosdb_definition = optional(map(object({
      existing_resource_id         = optional(string, null)
      private_dns_zone_resource_id = optional(string, null)
      enable_diagnostic_settings   = optional(bool, true)
      name                         = optional(string)
      secondary_regions = optional(list(object({
        location          = string
        zone_redundant    = optional(bool, true)
        failover_priority = optional(number, 0)
      })), [])
      public_network_access_enabled    = optional(bool, false)
      analytical_storage_enabled       = optional(bool, true)
      automatic_failover_enabled       = optional(bool, true)
      local_authentication_disabled    = optional(bool, true)
      partition_merge_enabled          = optional(bool, false)
      multiple_write_locations_enabled = optional(bool, false)
      analytical_storage_config = optional(object({
        schema_type = string
      }), null)
      consistency_policy = optional(object({
        max_interval_in_seconds = optional(number, 300)
        max_staleness_prefix    = optional(number, 100001)
        consistency_level       = optional(string, "Session")
      }), {})
      backup = optional(object({
        retention_in_hours  = optional(number)
        interval_in_minutes = optional(number)
        storage_redundancy  = optional(string)
        type                = optional(string)
        tier                = optional(string)
      }), {})
      capabilities = optional(set(object({
        name = string
      })), [])
      capacity = optional(object({
        total_throughput_limit = optional(number, -1)
      }), {})
      cors_rule = optional(object({
        allowed_headers    = set(string)
        allowed_methods    = set(string)
        allowed_origins    = set(string)
        exposed_headers    = set(string)
        max_age_in_seconds = optional(number, null)
      }), null)
      role_assignments = optional(map(object({
        role_definition_id_or_name             = string
        principal_id                           = string
        description                            = optional(string, null)
        skip_service_principal_aad_check       = optional(bool, false)
        condition                              = optional(string, null)
        condition_version                      = optional(string, null)
        delegated_managed_identity_resource_id = optional(string, null)
        principal_type                         = optional(string, null)
      })), {})
      tags = optional(map(string), {})
    })), {})

    key_vault_definition = optional(map(object({
      existing_resource_id         = optional(string, null)
      name                         = optional(string)
      private_dns_zone_resource_id = optional(string, null)
      enable_diagnostic_settings   = optional(bool, true)
      sku                          = optional(string, "standard")
      tenant_id                    = optional(string)
      role_assignments = optional(map(object({
        role_definition_id_or_name             = string
        principal_id                           = string
        description                            = optional(string, null)
        skip_service_principal_aad_check       = optional(bool, false)
        condition                              = optional(string, null)
        condition_version                      = optional(string, null)
        delegated_managed_identity_resource_id = optional(string, null)
        principal_type                         = optional(string, null)
      })), {})
      tags = optional(map(string), {})
    })), {})

    law_definition = optional(map(object({
      existing_resource_id = optional(string)
      name                 = optional(string)
      retention            = optional(number, 30)
      sku                  = optional(string, "PerGB2018")
      tags                 = optional(map(string), {})
    })), {})

    storage_account_definition = optional(map(object({
      existing_resource_id       = optional(string, null)
      enable_diagnostic_settings = optional(bool, true)
      name                       = optional(string, null)
      account_kind               = optional(string, "StorageV2")
      account_tier               = optional(string, "Standard")
      account_replication_type   = optional(string, "ZRS")
      endpoints = optional(map(object({
        type                         = string
        private_dns_zone_resource_id = optional(string, null)
        })), {
        blob = {
          type = "blob"
        }
      })
      access_tier               = optional(string, "Hot")
      shared_access_key_enabled = optional(bool, false)
      role_assignments = optional(map(object({
        role_definition_id_or_name             = string
        principal_id                           = string
        description                            = optional(string, null)
        skip_service_principal_aad_check       = optional(bool, false)
        condition                              = optional(string, null)
        condition_version                      = optional(string, null)
        delegated_managed_identity_resource_id = optional(string, null)
        principal_type                         = optional(string, null)
      })), {})
      tags = optional(map(string), {})
    })), {})
  })
  default     = {}
  description = <<DESCRIPTION
Configuration object for the Azure AI Foundry deployment (hub, projects, and Bring Your Own Resources).

- `create_byor` - (Optional) Whether to create BYOR resources managed by this module. Default is true.
- `purge_on_destroy` - (Optional) Whether to purge soft-deleteâ€“capable resources on destroy. Default is false.

- `ai_foundry` - (Optional) Azure AI Foundry hub settings.
  - `name` - (Optional) Name of the hub. If not provided, a name will be generated.
  - `disable_local_auth` - (Optional) Whether to disable local authentication. Default is false.
  - `allow_project_management` - (Optional) Whether project management is allowed from the hub. Default is true.
  - `create_ai_agent_service` - (Optional) Whether to create the AI Agent service in the hub. Default is false.
  - `private_dns_zone_resource_ids` - (Optional) List of private DNS zone resource IDs for hub endpoints. Default is [].
  - `sku` - (Optional) The SKU for the hub. Default is "S0".
  - `role_assignments` - (Optional) Map of role assignments on the hub. The map key is deliberately arbitrary to avoid plan-time unknown key issues.
    - `role_definition_id_or_name` - Role definition ID or name to assign.
    - `principal_id` - Principal ID for the assignment.
    - `description` - (Optional) Description of the role assignment.
    - `skip_service_principal_aad_check` - (Optional) Whether to skip AAD check for service principals. Default is false.
    - `condition` - (Optional) Condition for the role assignment.
    - `condition_version` - (Optional) Version of the condition.
    - `delegated_managed_identity_resource_id` - (Optional) Resource ID of the delegated managed identity.
    - `principal_type` - (Optional) Type of the principal (User, Group, ServicePrincipal).

- `ai_model_deployments` - (Optional) Map of model deployment configurations to create. The map key is arbitrary.
  - `name` - The name of the deployment.
  - `rai_policy_name` - (Optional) Responsible AI policy name applied to the deployment.
  - `version_upgrade_option` - (Optional) Version upgrade option for the model. Default is "OnceNewDefaultVersionAvailable".
  - `model` - Model specification.
    - `format` - Model format (e.g., OpenAI, OSS foundation model format).
    - `name` - Model name.
    - `version` - Model version.
  - `scale` - Scale configuration for the deployment.
    - `capacity` - (Optional) Capacity value for the selected SKU family/size.
    - `family` - (Optional) SKU family.
    - `size` - (Optional) SKU size.
    - `tier` - (Optional) SKU tier.
    - `type` - Scale type (e.g., Standard/ProvisionedManaged/Serverless, depending on service).

- `ai_projects` - (Optional) Map of AI Project configurations to create. The map key is arbitrary.
  - `name` - Resource name of the project.
  - `sku` - (Optional) SKU for the project. Default is "S0".
  - `display_name` - Display name for the project.
  - `description` - Description of the project.
  - `create_project_connections` - (Optional) Whether to create project-level connections to dependent services. Default is false.
  - `cosmos_db_connection` - (Optional) Connection to Cosmos DB.
    - `existing_resource_id` - (Optional) Resource ID of an existing Cosmos DB to connect.
    - `new_resource_map_key` - (Optional) Key referencing a new resource from `cosmosdb_definition`.
  - `ai_search_connection` - (Optional) Connection to Azure AI Search.
    - `existing_resource_id` - (Optional) Resource ID of an existing AI Search to connect.
    - `new_resource_map_key` - (Optional) Key referencing a new resource from `ai_search_definition`.
  - `key_vault_connection` - (Optional) Connection to Key Vault.
    - `existing_resource_id` - (Optional) Resource ID of an existing Key Vault to connect.
    - `new_resource_map_key` - (Optional) Key referencing a new resource from `key_vault_definition`.
  - `storage_account_connection` - (Optional) Connection to Storage Account.
    - `existing_resource_id` - (Optional) Resource ID of an existing Storage Account to connect.
    - `new_resource_map_key` - (Optional) Key referencing a new resource from `storage_account_definition`.

- Bring Your Own Resources (BYOR) definitions
  - `ai_search_definition` - (Optional) Map defining one or more Azure AI Search services.
    - `existing_resource_id` - (Optional) Resource ID of an existing service to reuse.
    - `name` - (Optional) Name of the service if creating new.
    - `private_dns_zone_resource_id` - (Optional) Private DNS zone resource ID for the service.
    - `enable_diagnostic_settings` - (Optional) Whether diagnostic settings are enabled. Default is true.
    - `sku` - (Optional) Service SKU. Default is "standard".
    - `local_authentication_enabled` - (Optional) Whether local auth is enabled. Default is true.
    - `partition_count` - (Optional) Number of partitions. Default is 1.
    - `replica_count` - (Optional) Number of replicas. Default is 2.
    - `semantic_search_sku` - (Optional) Semantic search SKU. Default is "standard".
    - `semantic_search_enabled` - (Optional) Whether semantic search is enabled. Default is false.
    - `hosting_mode` - (Optional) Hosting mode. Default is "default".
    - `tags` - (Optional) Map of tags for the service.
    - `role_assignments` - (Optional) Map of role assignments on the service.
      - `role_definition_id_or_name` - Role definition ID or name to assign.
      - `principal_id` - Principal ID for the assignment.
      - `description` - (Optional) Description of the role assignment.
      - `skip_service_principal_aad_check` - (Optional) Whether to skip AAD check for service principals. Default is false.
      - `condition` - (Optional) Condition for the role assignment.
      - `condition_version` - (Optional) Version of the condition.
      - `delegated_managed_identity_resource_id` - (Optional) Resource ID of the delegated managed identity.
      - `principal_type` - (Optional) Type of the principal (User, Group, ServicePrincipal).
    - `enable_telemetry` - (Optional) Whether telemetry is enabled for this resource. Default is true.

  - `cosmosdb_definition` - (Optional) Map defining one or more Azure Cosmos DB accounts.
    - `existing_resource_id` - (Optional) Resource ID of an existing account to reuse.
    - `private_dns_zone_resource_id` - (Optional) Private DNS zone resource ID.
    - `enable_diagnostic_settings` - (Optional) Whether diagnostic settings are enabled. Default is true.
    - `name` - (Optional) Name of the account if creating new.
    - `secondary_regions` - (Optional) List of secondary regions for geo-replication. Default is [].
      - `location` - Azure region name for the secondary location.
      - `zone_redundant` - (Optional) Whether zone redundancy is enabled. Default is true.
      - `failover_priority` - (Optional) Failover priority. Default is 0.
    - `public_network_access_enabled` - (Optional) Whether public network access is enabled. Default is false.
    - `analytical_storage_enabled` - (Optional) Whether analytical storage is enabled. Default is true.
    - `automatic_failover_enabled` - (Optional) Whether automatic failover is enabled. Default is true.
    - `local_authentication_disabled` - (Optional) Whether local authentication is disabled. Default is true.
    - `partition_merge_enabled` - (Optional) Whether partition merge is enabled. Default is false.
    - `multiple_write_locations_enabled` - (Optional) Whether multiple write locations are enabled. Default is false.
    - `analytical_storage_config` - (Optional) Analytical storage configuration. Default is null.
      - `schema_type` - Schema type for analytical storage.
    - `consistency_policy` - (Optional) Consistency policy configuration.
      - `max_interval_in_seconds` - (Optional) Max staleness interval in seconds. Default is 300.
      - `max_staleness_prefix` - (Optional) Max staleness prefix. Default is 100001.
      - `consistency_level` - (Optional) Consistency level. Default is "Session".
    - `backup` - (Optional) Backup configuration.
      - `retention_in_hours` - (Optional) Backup retention in hours.
      - `interval_in_minutes` - (Optional) Backup interval in minutes.
      - `storage_redundancy` - (Optional) Storage redundancy for backups.
      - `type` - (Optional) Backup type.
      - `tier` - (Optional) Backup tier.
    - `capabilities` - (Optional) Set of capabilities to enable.
      - `name` - Capability name.
    - `capacity` - (Optional) Capacity configuration.
      - `total_throughput_limit` - (Optional) Total throughput limit. Default is -1 (unlimited).
    - `cors_rule` - (Optional) CORS rule configuration. Default is null.
      - `allowed_headers` - Set of allowed headers.
      - `allowed_methods` - Set of allowed methods.
      - `allowed_origins` - Set of allowed origins.
      - `exposed_headers` - Set of exposed headers.
      - `max_age_in_seconds` - (Optional) Maximum age in seconds for CORS.
    - `role_assignments` - (Optional) Map of role assignments on the account.
      - `role_definition_id_or_name` - Role definition ID or name to assign.
      - `principal_id` - Principal ID for the assignment.
      - `description` - (Optional) Description of the role assignment.
      - `skip_service_principal_aad_check` - (Optional) Whether to skip AAD check for service principals. Default is false.
      - `condition` - (Optional) Condition for the role assignment.
      - `condition_version` - (Optional) Version of the condition.
      - `delegated_managed_identity_resource_id` - (Optional) Resource ID of the delegated managed identity.
      - `principal_type` - (Optional) Type of the principal (User, Group, ServicePrincipal).
    - `tags` - (Optional) Map of tags for the account.

  - `key_vault_definition` - (Optional) Map defining one or more Azure Key Vaults.
    - `existing_resource_id` - (Optional) Resource ID of an existing vault to reuse.
    - `name` - (Optional) Name of the vault if creating new.
    - `private_dns_zone_resource_id` - (Optional) Private DNS zone resource ID.
    - `enable_diagnostic_settings` - (Optional) Whether diagnostic settings are enabled. Default is true.
    - `sku` - (Optional) Vault SKU. Default is "standard".
    - `tenant_id` - (Optional) Tenant ID for the Key Vault.
    - `role_assignments` - (Optional) Map of role assignments on the vault.
      - `role_definition_id_or_name` - Role definition ID or name to assign.
      - `principal_id` - Principal ID for the assignment.
      - `description` - (Optional) Description of the role assignment.
      - `skip_service_principal_aad_check` - (Optional) Whether to skip AAD check for service principals. Default is false.
      - `condition` - (Optional) Condition for the role assignment.
      - `condition_version` - (Optional) Version of the condition.
      - `delegated_managed_identity_resource_id` - (Optional) Resource ID of the delegated managed identity.
      - `principal_type` - (Optional) Type of the principal (User, Group, ServicePrincipal).
    - `tags` - (Optional) Map of tags for the vault.

  - `law_definition` - (Optional) Map defining one or more Log Analytics Workspaces.
    - `existing_resource_id` - (Optional) Resource ID of an existing workspace to reuse.
    - `name` - (Optional) Name of the workspace if creating new.
    - `retention` - (Optional) Data retention in days. Default is 30.
    - `sku` - (Optional) Workspace SKU. Default is "PerGB2018".
    - `tags` - (Optional) Map of tags for the workspace.

  - `storage_account_definition` - (Optional) Map defining one or more Storage Accounts.
    - `existing_resource_id` - (Optional) Resource ID of an existing account to reuse.
    - `enable_diagnostic_settings` - (Optional) Whether diagnostic settings are enabled. Default is true.
    - `name` - (Optional) Name of the account if creating new.
    - `account_kind` - (Optional) Storage account kind. Default is "StorageV2".
    - `account_tier` - (Optional) Storage account tier. Default is "Standard".
    - `account_replication_type` - (Optional) Replication type. Default is "ZRS".
    - `endpoints` - (Optional) Map of subservice endpoints to enable. Defaults to enabling the `blob` endpoint.
      - map key - Endpoint name (e.g., `blob`).
      - `type` - Endpoint type (e.g., "blob").
      - `private_dns_zone_resource_id` - (Optional) Private DNS zone resource ID for the endpoint.
    - `access_tier` - (Optional) Access tier for the account. Default is "Hot".
    - `shared_access_key_enabled` - (Optional) Whether shared access keys are enabled. Default is false.
    - `role_assignments` - (Optional) Map of role assignments on the storage account.
      - `role_definition_id_or_name` - Role definition ID or name to assign.
      - `principal_id` - Principal ID for the assignment.
      - `description` - (Optional) Description of the role assignment.
      - `skip_service_principal_aad_check` - (Optional) Whether to skip AAD check for service principals. Default is false.
      - `condition` - (Optional) Condition for the role assignment.
      - `condition_version` - (Optional) Version of the condition.
      - `delegated_managed_identity_resource_id` - (Optional) Resource ID of the delegated managed identity.
      - `principal_type` - (Optional) Type of the principal (User, Group, ServicePrincipal).
    - `tags` - (Optional) Map of tags for the storage account.

This object supports both creating new resources and connecting to existing ones, enabling flexible deployment scenarios across the hub, projects, and dependent services.
DESCRIPTION
}

# ============================================================================
# AI LANDING ZONE MODULE VARIABLES
# ============================================================================

variable "deploy_landing_zone" {
  type        = bool
  default     = false
  description = <<DESCRIPTION
Whether to deploy the full AI Landing Zone infrastructure.

When enabled (true), the ai_landing_zone module will be included. 
Control individual components via their respective deploy flags 
(e.g., bastion_definition.deploy, firewall_definition.deploy).
DESCRIPTION
}

variable "landing_zone_location" {
  type        = string
  default     = null
  description = <<DESCRIPTION
Azure region for the AI Landing Zone resources.

If not specified, will use the same location as the main module (var.location).
DESCRIPTION
}

variable "landing_zone_resource_group_name" {
  type        = string
  default     = null
  description = <<DESCRIPTION
Name of the resource group for the AI Landing Zone.

If not specified, will default to "{resource_group_name}-lz".
DESCRIPTION
}

variable "landing_zone_ai_foundry_definition" {
  type        = any
  default     = {}
  description = <<DESCRIPTION
AI Foundry configuration for the Landing Zone module.

This is separate from the main ai_foundry_definition to allow different 
configurations for the AI Service module vs the Landing Zone module.
See terraform.tfvars.standalone for the full schema.
DESCRIPTION
}

variable "vnet_definition" {
  type = object({
    name          = optional(string, null)
    address_space = optional(string, "192.168.0.0/20")
    dns_servers   = optional(list(string), [])
    existing_byo_vnet = optional(map(object({
      vnet_resource_id    = string
      firewall_ip_address = optional(string)
    })), {})
  })
  default     = null
  description = <<DESCRIPTION
Virtual Network configuration for the AI Landing Zone.

Option 1: Create new VNet
- `name` - Name of the Virtual Network.
- `address_space` - CIDR block for the VNet (e.g., "192.168.32.0/20").
- `dns_servers` - (Optional) List of custom DNS server IPs. Empty = use Azure default DNS.

Option 2: Use existing VNet (BYO VNet)
- `existing_byo_vnet` - Map to configure use of an existing Virtual Network.
  - `vnet_resource_id` - Resource ID of the existing VNet. Can be overridden by var.landing_zone_vnet_resource_id.
  - `firewall_ip_address` - (Optional) IP address of the firewall for route table configuration.

Note: address_space must be from 192.168.0.0/16 range for AI Foundry capability host injection.
DESCRIPTION
}

variable "landing_zone_vnet_resource_id" {
  type        = string
  default     = null
  description = <<DESCRIPTION
(Optional) Resource ID of an existing VNet to use for the AI Landing Zone (BYO VNet).
This can be set via environment variable: TF_VAR_landing_zone_vnet_resource_id

When set, this overrides any vnet_resource_id specified in vnet_definition.existing_byo_vnet.
The ai_landing_zone module will create required subnets in this existing VNet.

Example: /subscriptions/{sub-id}/resourceGroups/{rg}/providers/Microsoft.Network/virtualNetworks/{vnet-name}
DESCRIPTION
}

variable "bastion_definition" {
  type = object({
    deploy = optional(bool, false)
    name   = optional(string, null)
    sku    = optional(string, "Standard")
    zones  = optional(list(string), ["1", "2", "3"])
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
Azure Bastion configuration for secure VM access.

- `deploy` - Whether to deploy Azure Bastion.
- `name` - Name of the Bastion host.
- `sku` - Bastion SKU (Basic or Standard).
- `zones` - Availability zones for deployment.
DESCRIPTION
}

variable "firewall_definition" {
  type = object({
    deploy = optional(bool, false)
    name   = optional(string, null)
    sku    = optional(string, "AZFW_VNet")
    tier   = optional(string, "Standard")
    zones  = optional(list(string), ["1", "2", "3"])
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
Azure Firewall configuration.

- `deploy` - Whether to deploy Azure Firewall.
- `name` - Name of the Firewall.
- `sku` - Firewall SKU.
- `tier` - Firewall tier (Standard or Premium).
- `zones` - Availability zones for deployment.
DESCRIPTION
}

variable "app_gateway_definition" {
  type = object({
    deploy       = optional(bool, false)
    name         = optional(string, null)
    http2_enable = optional(bool, true)
    sku = optional(object({
      name     = optional(string, "WAF_v2")
      tier     = optional(string, "WAF_v2")
      capacity = optional(number, 2)
    }), {})
    backend_address_pools = optional(map(object({
      name = string
    })), {})
    backend_http_settings = optional(map(object({
      name     = string
      port     = number
      protocol = string
    })), {})
    frontend_ports = optional(map(object({
      name = string
      port = number
    })), {})
    http_listeners = optional(map(object({
      name               = string
      frontend_port_name = string
    })), {})
    request_routing_rules = optional(map(object({
      name                       = string
      rule_type                  = string
      http_listener_name         = string
      backend_address_pool_name  = string
      backend_http_settings_name = string
      priority                   = number
    })), {})
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
Application Gateway configuration with WAF.

- `deploy` - Whether to deploy Application Gateway.
- `name` - Name of the Application Gateway.
- `http2_enable` - Enable HTTP/2.
- `sku` - SKU configuration (name, tier, capacity).
- `backend_address_pools` - Backend pools configuration.
- `backend_http_settings` - HTTP settings for backends.
- `frontend_ports` - Frontend port configurations.
- `http_listeners` - HTTP listener configurations.
- `request_routing_rules` - Routing rules configuration.
DESCRIPTION
}

variable "genai_container_registry_definition" {
  type = object({
    deploy                  = optional(bool, false)
    name                    = optional(string, null)
    sku                     = optional(string, "Basic")
    zone_redundancy_enabled = optional(bool, true)
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
Azure Container Registry configuration for GenAI workloads.

- `deploy` - Whether to deploy Container Registry.
- `name` - Name of the Container Registry.
- `sku` - Registry SKU (Basic, Standard, Premium).
- `zone_redundancy_enabled` - Enable zone redundancy.
DESCRIPTION
}

variable "container_app_environment_definition" {
  type = object({
    deploy                         = optional(bool, false)
    name                           = optional(string, null)
    internal_load_balancer_enabled = optional(bool, true)
    zone_redundancy_enabled        = optional(bool, true)
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
Container App Environment configuration.

- `deploy` - Whether to deploy Container App Environment.
- `name` - Name of the environment.
- `internal_load_balancer_enabled` - Use internal load balancer.
- `zone_redundancy_enabled` - Enable zone redundancy.
DESCRIPTION
}

variable "genai_app_configuration_definition" {
  type = object({
    deploy                   = optional(bool, false)
    name                     = optional(string, null)
    sku                      = optional(string, "standard")
    local_auth_enabled       = optional(bool, false)
    purge_protection_enabled = optional(bool, true)
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
Azure App Configuration for GenAI settings.

- `deploy` - Whether to deploy App Configuration.
- `name` - Name of the App Configuration.
- `sku` - SKU (free or standard).
- `local_auth_enabled` - Enable local authentication.
- `purge_protection_enabled` - Enable purge protection.
DESCRIPTION
}

variable "genai_key_vault_definition" {
  type = object({
    name                          = optional(string, null)
    sku                           = optional(string, "standard")
    public_network_access_enabled = optional(bool, false)
  })
  default     = {}
  description = <<DESCRIPTION
Key Vault configuration for GenAI secrets.

- `name` - Name of the Key Vault.
- `sku` - Vault SKU (standard or premium).
- `public_network_access_enabled` - Enable public network access.
DESCRIPTION
}

variable "genai_storage_account_definition" {
  type = object({
    deploy                    = optional(bool, false)
    name                      = optional(string, null)
    account_tier              = optional(string, "Standard")
    account_replication_type  = optional(string, "GRS")
    shared_access_key_enabled = optional(bool, true)
    endpoint_types            = optional(list(string), ["blob"])
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
Storage Account configuration for GenAI data.

- `deploy` - Whether to deploy Storage Account.
- `name` - Name of the Storage Account (must be globally unique).
- `account_tier` - Account tier (Standard or Premium).
- `account_replication_type` - Replication type (LRS, GRS, ZRS, etc.).
- `shared_access_key_enabled` - Enable shared access keys.
- `endpoint_types` - List of endpoint types to enable.
DESCRIPTION
}

variable "genai_cosmosdb_definition" {
  type = object({
    deploy                        = optional(bool, false)
    name                          = optional(string, null)
    analytical_storage_enabled    = optional(bool, true)
    automatic_failover_enabled    = optional(bool, false)
    local_authentication_disabled = optional(bool, true)
    secondary_regions             = optional(list(any), [])
    enable_diagnostic_settings    = optional(bool, false) # Disabled by default due to upstream module bug
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
Cosmos DB configuration for GenAI data.

- `deploy` - Whether to deploy Cosmos DB.
- `name` - Name of the Cosmos DB account.
- `analytical_storage_enabled` - Enable analytical storage.
- `automatic_failover_enabled` - Enable automatic failover.
- `local_authentication_disabled` - Disable local authentication.
- `secondary_regions` - List of secondary regions for geo-replication.
- `enable_diagnostic_settings` - Enable diagnostic settings. Default false due to upstream module bug with metric_categories.
DESCRIPTION
}

variable "ks_ai_search_definition" {
  type = object({
    deploy              = optional(bool, false)
    name                = optional(string, null)
    sku                 = optional(string, "standard")
    partition_count     = optional(number, 1)
    replica_count       = optional(number, 2)
    semantic_search_sku = optional(string, "standard")
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
AI Search configuration for Knowledge Sources.

- `deploy` - Whether to deploy AI Search.
- `name` - Name of the AI Search service.
- `sku` - Service SKU.
- `partition_count` - Number of partitions.
- `replica_count` - Number of replicas.
- `semantic_search_sku` - Semantic search SKU.
DESCRIPTION
}

variable "ks_bing_grounding_definition" {
  type = object({
    deploy = optional(bool, false)
    name   = optional(string, null)
    sku    = optional(string, "G1")
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
Bing Grounding Service configuration for Knowledge Sources.

- `deploy` - Whether to deploy Bing Grounding.
- `name` - Name of the service.
- `sku` - Service SKU.
DESCRIPTION
}

variable "jumpvm_definition" {
  type = object({
    deploy = optional(bool, false)
    name   = optional(string, null)
    sku    = optional(string, "Standard_B2s")
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
Jump VM configuration for management access.

- `deploy` - Whether to deploy Jump VM.
- `name` - Name of the VM.
- `sku` - VM size/SKU.
DESCRIPTION
}

variable "buildvm_definition" {
  type = object({
    deploy = optional(bool, false)
    name   = optional(string, null)
    sku    = optional(string, "Standard_B2s")
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
Build VM configuration for CI/CD workloads.

- `deploy` - Whether to deploy Build VM.
- `name` - Name of the VM.
- `sku` - VM size/SKU.
DESCRIPTION
}

variable "landing_zone_private_dns_zones" {
  type = object({
    existing_zones_resource_group_resource_id = optional(string, null)
    allow_internet_resolution_fallback        = optional(bool, false)
    network_links                             = optional(map(any), {})
  })
  default     = {}
  description = <<DESCRIPTION
Private DNS Zones configuration for the AI Landing Zone.

- `existing_zones_resource_group_resource_id` - Resource ID of existing DNS zones resource group.
- `allow_internet_resolution_fallback` - Allow fallback to internet DNS resolution.
- `network_links` - Map of network links to create.
DESCRIPTION
}

variable "landing_zone_private_dns_zone_existing_rg_id" {
  type        = string
  default     = null
  nullable    = true
  description = <<DESCRIPTION
Standalone variable for the resource ID of the existing DNS zones resource group for the AI Landing Zone.
This can be set via TF_VAR_landing_zone_private_dns_zone_existing_rg_id environment variable.
Used as fallback when landing_zone_private_dns_zones.existing_zones_resource_group_resource_id is not set.
DESCRIPTION
}

variable "apim_definition" {
  type = object({
    deploy          = optional(bool, false)
    name            = optional(string, null)
    publisher_email = optional(string, "DoNotReply@example.com")
    publisher_name  = optional(string, "Azure API Management")
    sku_root        = optional(string, "Developer")
    sku_capacity    = optional(number, 1)
    tags            = optional(map(string), {})
    additional_locations = optional(list(object({
      location             = string
      capacity             = optional(number, null)
      zones                = optional(list(string), null)
      public_ip_address_id = optional(string, null)
      gateway_disabled     = optional(bool, null)
      virtual_network_configuration = optional(object({
        subnet_id = string
      }), null)
    })), [])
    certificate = optional(list(object({
      encoded_certificate  = string
      store_name           = string
      certificate_password = optional(string, null)
    })), [])
    client_certificate_enabled = optional(bool, false)
    hostname_configuration     = optional(any, null)
    min_api_version            = optional(string, null)
    notification_sender_email  = optional(string, null)
    protocols = optional(object({
      enable_http2 = optional(bool, false)
    }), null)
    role_assignments = optional(map(object({
      role_definition_id_or_name             = string
      principal_id                           = string
      description                            = optional(string, null)
      skip_service_principal_aad_check       = optional(bool, false)
      condition                              = optional(string, null)
      condition_version                      = optional(string, null)
      delegated_managed_identity_resource_id = optional(string, null)
      principal_type                         = optional(string, null)
    })), {})
    sign_in = optional(object({
      enabled = bool
    }), null)
    sign_up = optional(object({
      enabled = bool
      terms_of_service = object({
        consent_required = bool
        enabled          = bool
        text             = optional(string, null)
      })
    }), null)
    tenant_access = optional(object({
      enabled = bool
    }), null)
  })
  default = {
    deploy = false
  }
  description = <<DESCRIPTION
Azure API Management configuration.

- `deploy` - Whether to deploy APIM. Default is false.
- `name` - Name of the APIM instance. Auto-generated if not provided.
- `publisher_email` - Publisher email address.
- `publisher_name` - Publisher name.
- `sku_root` - SKU tier: Developer, Basic, Standard, Premium, Consumption. Default is Developer.
- `sku_capacity` - Number of scale units. Default is 1 for Developer SKU.
- `tags` - Additional tags for the APIM resource.
- `additional_locations` - List of additional Azure regions for multi-region deployment.
- `certificate` - List of certificates to upload.
- `client_certificate_enabled` - Enable client certificate authentication.
- `hostname_configuration` - Custom hostname configurations.
- `min_api_version` - Minimum API version.
- `notification_sender_email` - Notification sender email.
- `protocols` - Protocol settings (HTTP/2).
- `role_assignments` - RBAC role assignments.
- `sign_in` - Developer portal sign-in settings.
- `sign_up` - Developer portal sign-up settings.
- `tenant_access` - Tenant access settings.
DESCRIPTION
}


variable "client_id" {
  description = "Azure client ID for the service principal"
  type        = string
  sensitive   = true
  nullable    = false
}
variable "tenant_id" {
  description = "Azure tenant ID"
  type        = string
  nullable    = false
}
variable "use_oidc" {
  description = "Whether to use OIDC for authentication"
  type        = bool
  default     = true
}
